plugins {
    alias libs.plugins.fabric.loom
    id "maven-publish"
}

version = "$mod_version+$target_version"
group = maven_group

base {
    archivesName = archives_name
}

repositories {
    maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots" }
}

loom {
    decompilers {
        vineflower {
            options.putAll(["mark-corresponding-synthetics": "1", "ind": "    "])
        }
    }
    mixin {
        useLegacyMixinAp = false
    }
    accessWidenerPath = file("src/main/resources/starlight.accesswidener")
}

dependencies {
    minecraft libs.minecraft
    mappings loom.officialMojangMappings()
    modImplementation libs.fabric.loader
    vineflowerDecompilerClasspath libs.vineflower
}

processResources {
    filesMatching "fabric.mod.json", {
        expand "version": version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) it.options.release.set(8)
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}

jar {
    from "LICENSE"
}

publishing {
    publications.create("mavenJava", MavenPublication) {
        from components.java
    }
    repositories {}
}
